description = 'GAE Selenium testing'

apply plugin: 'appengine'
apply plugin: 'war'

appengine {
    disableUpdateCheck = true
    daemon = server_daemon
}

war {
    webXml = file('src/main/resources/WEB-INF/web.xml')
    from "src/main/resources"
}

dependencies {
    compile libs.guice
    compile "com.google.inject.extensions:guice-assistedinject:${guice_version}"
    compile "com.google.inject.extensions:guice-servlet:${guice_version}"
    compile project(':test-app')
    compile project(':test')
    compile project(':controller')
    compile project(':command-jpa-gae')
    testCompile project(':test-bot')
    testCompile project(path:':selenium-test', configuration: "testCompile")
}

task unpackTest(dependsOn: ":selenium-test:packageTests") << {
    mkdir "$buildDir/selenium-test"
    def myTests = file("../selenium-test/build/libs/selenium-test-${version}-tests.jar")
    ant.unjar(dest: "$buildDir/selenium-test", src: myTests)
}

task integrationTest(type: Test, dependsOn: [unpackTest, appengineRun]) {
    testClassesDir  file("$buildDir/selenium-test")
    systemProperties['bot.secret'] = "SECRET"
    systemProperties['endpoint'] = "localhost:8080"
    systemProperties['peer.jar'] = "../test-bot/build/libs/test-bot-${version}.jar"
    testLogging.showStandardStreams = true
}

appengineStop.dependsOn integrationTest
